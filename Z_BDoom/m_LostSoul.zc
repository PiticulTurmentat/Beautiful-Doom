Class BD_LostSoul : LostSoul {
	BD_BaseFlare flare;
	Default {
		translation "80:95=48:63", "96:111=64:79", "236:239=0:0", "3:3=13:13";
		renderstyle "Normal";
		+NOBLOOD
		+FORCEXYBILLBOARD
		+FLOATBOB
		FloatBobStrength 0.3;
		speed 1.334;
	}
	override void Tick() {
		if (target)
			bFLOATBOB = false;
		if (isFrozen())
			return;
		if (bdoom_flames && health > 0) {
			if (InstateSequence(curstate,FindState("Missile")))
				A_SpawnItemEx ("BD_ParticleFire_Red",-10,frandom(-4,4),frandom(10,28), 0,frandom(-1,1),frandom(1,1.25));
			else
				A_SpawnItemEx ("BD_ParticleFire_Red",frandom(-4,-8),frandom(-4,4),frandom(10,28), frandom(-0.8,-1),frandom(-1,1),frandom(1,1.25));
		}
		super.Tick();
	}
	states	{
	Spawn:
		TNT1 A 0 NoDelay A_JumpIf(bdoom_flames == false,"SpawnVanilla");
		LOSL A 10 A_Look();
		loop;
	See:
		TNT1 A 0 A_JumpIf(bdoom_flames == false,"SeeVanilla");
		LOSL A 1 A_Chase();
		LOSL AAAAA 1 A_Chase(null,null,CHF_DONTTURN);
		loop;
	Missile:
		TNT1 A 0 A_JumpIf(bdoom_flames == false,"MissileVanilla");
		LOSL B 10 A_FaceTarget();
		LOSL B 4 A_SkullAttack();
		LOSL B 8;
		goto missile+3;
	Pain:
		TNT1 A 0 A_JumpIf(bdoom_flames == false,"PainVanilla");
		LOSL E 3;
		LOSL F 3 A_Pain();
		goto See;
	Death:
		TNT1 A 0 { 
			scale.x *= randompick[bdsfx](-1,1);
		}
		TNT1 A 0 A_JumpIf(bdoom_flames == false,"DeathVanilla");
		LOSL GH 3 bright;
		LOSL I 3 bright A_Scream();
		LOSL JE 3 bright ;
		TNT1 A 0 {
			A_SetRenderstyle(0.65,Style_ADD);
			if (bdoom_gibs) {
				A_SpawnItemEx("BD_LS_Horn",	frandom(-4,4),frandom(-4,4),frandom(5,24),	frandom(-4,4),frandom(-4,4),frandom(2,5),	0,	0, 180);
				A_SpawnItemEx("BD_LS_Horn",	frandom(-4,4),frandom(-4,4),frandom(5,24),	frandom(-4,4),frandom(-4,4),frandom(2,5),	0,	0, 180);
				A_SpawnItemEx("BD_LS_Skull",	frandom(-4,4),frandom(-4,4),frandom(5,24),	frandom(-4,4),frandom(-4,4),frandom(2,5),	0,	0, 128);
				for (int i = 5; i > 0; i--)
					A_SpawnItemEx("BD_LS_Shard",	frandom(-4,4),frandom(-4,4),frandom(5,24),	frandom(-4,4),frandom(-4,4),frandom(2,5),	0,	0, 128);
				for (int i = 4; i > 0; i--)
					A_SpawnItemEx("BD_LS_FlameShard",	frandom(-4,4),frandom(-4,4),frandom(5,24),	frandom(-4,4),frandom(-4,4),frandom(2,5),	0,	0, 128);
			}
		}
		ssou f 3 bright A_noblocking();
		ssou ghij 3 bright;
		stop;
	Raise:
		stop;

	SpawnVanilla:
		ZKUL A 5 Bright A_Look();
		ZKUL B 5 bright;
		ZKUL C 5 bright A_Look();
		ZKUL D 5 bright;
		goto Spawn;
	seeVanilla:
		ZKUL a 3 bright A_Chase();
		ZKUL b 3 bright A_Chase(null,null);
		ZKUL c 3 bright A_Chase();
		ZKUL d 3 bright A_Chase(null,null);
		goto See;
	missileVanilla:
		ssku abcda 2 bright A_FaceTarget();
		ssku b 4 bright A_SkullAttack();
		ssku abcd 2 bright;
		goto missileVanilla+6;
	PainVanilla:
		LSTP A 3 Bright;
		LSTP B 3 Bright A_Pain();
		Goto See;
	deathVanilla:
		ssou ab 3 bright;
		ssou c 3 bright A_Scream();
		ssou de 3 bright;
		TNT1 A 0 A_SetRenderstyle(0.65,Style_ADD);
		ssou f 3 bright A_noblocking();
		ssou ghij 3 bright;
		stop;
	}
}

Class BD_LS_FlameShard : BD_SmallDebris {
	protected double fade;
	Default {
		BD_SmallDebris.dbrake 0.3;
	}
	override void PostBeginPlay() {
		super.PostBeginPlay();
		alpha = frandom(0.5,1);
		fade = frandom[ls](0.01,0.03);
	}
	states	{
	Spawn:
		TNT1 A 1 {
			A_SpawnItemEx("BD_LSFireParticle",0,frandom(-3,3),frandom(-3,3),0,0,frandom(0.5,0.9));
			A_FadeOut(fade);
		}
		loop;
	}
}

Class BD_LSFireParticle : BD_BaseFlare {
	Default {
		scale 0.28;
	}
	states	{
	Spawn:
		FIRP ABCDEFGHIJ 2;
		stop;
	}
}